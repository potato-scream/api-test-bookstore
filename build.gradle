plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version '6.0.0-m2'
    id "com.diffplug.spotless" version "6.25.0"
}

repositories {
    mavenCentral()
}

def restAssuredVersion = "5.4.0",
    allureVersion = "2.27.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    testImplementation(
        "io.rest-assured:rest-assured:$restAssuredVersion",
        "io.rest-assured:json-schema-validator:$restAssuredVersion",
        "io.qameta.allure:allure-rest-assured:$allureVersion",
        "io.qameta.allure:allure-junit5:$allureVersion",
        "org.junit.jupiter:junit-jupiter:5.11.2",
        'com.github.javafaker:javafaker:1.0.2',
        "org.slf4j:slf4j-simple:2.0.7",
        "org.assertj:assertj-core:3.26.0",
        "org.aeonbits.owner:owner:1.0.12"
    )

}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}


spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

test.dependsOn spotlessCheck
